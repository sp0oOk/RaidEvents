plugins {
    id 'java'
}

group = 'me.spook'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        name = "spigotmc-repo"
        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        url = "https://repo.extendedclip.com/content/repositories/placeholderapi/"
    }
}

dependencies {
    compileOnly("org.spigotmc:spigot-api:1.8.8-R0.1-SNAPSHOT")
    compileOnly("me.clip:placeholderapi:2.10.9")
    compileOnly(files("dependencies/Factions.jar"))

    implementation("com.massivecraft:MassiveCore:2.13.7")
}

def targetJavaVersion = 8
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

tasks.build.doLast {
    def user = System.getProperty('user.name')
    def builtJar = file("build/libs/${project.name}-${project.version}.jar")

    if(user == "spook" && builtJar.exists()) {
        def serverDir = file("C:\\Users\\spook\\Desktop\\Minecraft Servers\\Minecraft Server\\plugins")
        copy {
            from builtJar
            into serverDir
        }
        println("Copied ${builtJar.name} to ${serverDir} for ${user}")
        return
    }

    println("Build to default location: ${builtJar} for ${user}")
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
